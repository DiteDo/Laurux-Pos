' Gambas class file

'----------------------------------------------------------------------------
'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'----------------------------------------------------------------------------
'################################################
'# nom du fichier           : LauruxConf.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 07 janvier 2010
'# Gestion du fichier de configuration
'################################################
'#######################################################
'# Modification Marc GUILLAUME <alarch@alarch.ath.cx>
'# pour utilisation d'un tunnel ssh de connexion MySQL
'# 07 janvier 2010
'########################################################
'
Public Hwnd As Fmenu
Public hcon As DB_Connection = Utils.db 'As New Connection
Public Soc As String
Public intitule As String
Public Fonte As Integer
Public Version As String
Public Hfile As File
Public d As String
'Static Public DB As New Connection
Public lnd As Integer
Public Lig As String
Public Key As String
Public iPos As Integer
Public rResult As Result

Public Sub _new()

  Dim Frmt As String[]
  Dim ssh As String

  Utils.Observers(Me)
  Frmt = Utils.FColr(Start.LocalSettings["/Coul/Fnets"])
  If Start.LocalSettings["/Soc" & Start.Societe & "/Coul_fen"] = 0 Then Me.Background = Val(Frmt[0])
  Music.Load(Start.Musique)
  'Me.Center
  Try ssh = Start.LocalSettings["/dbase/Con-ssh"]
  If ssh <> 0 And Not IsNull(ssh) Then
    Bssh.value = True
    Bssh_Click()
  Endif
  Host.Add(Start.LocalSettings["/dbase/Host"], 0)
  Host.Add(Start.LocalSettings["/dbase/Host2"], 1)
  Try Cnl.value = Start.LocalSettings["/dbase/Cnl"]
  If Cnl.value Then
    Host.Text = Start.LocalSettings["/dbase/Host2"]
  Else
    Host.Text = Start.LocalSettings["/dbase/Host"]
  Endif

End

'***********************************************************
'* La connexion se fait sans mot de passe, la clé publique
'* de l'utilisateur doit se trouver sur le serveur SQL
'* contacté en SSH
'* MG - 07 janvier 2010
'***********************************************************
Public Sub Button1_Click()

  Dim $ready_connect As Boolean

  $ready_connect = True
  Soc = "01"
  intitule = "  "
  Start.LocalSettings["/dbase/Version"] = Application.Name & " V" & Split(Application.Version, ".")[0] & "." & Split(Application.Version, ".")[1]
  '# -- Partie commune aux connexions avec ou sans tunnel
  Start.LocalSettings["/dbase/type"] = "mysql"
  Start.LocalSettings["/dbase/Name"] = "Laurux01"
  Start.LocalSettings["/dbase/Login"] = login.Text
  Start.LocalSettings["/dbase/Password"] = hexdblKey.crypt("en", Password.Text, "Laurux")
  Start.LocalSettings["/dbase/Port"] = Port.Text
  Start.LocalSettings["/dbase/Cnl"] = Cnl.value
  If Cnl.value Then
    Start.LocalSettings["/dbase/Host2"] = Host.Text
  Else
    Start.LocalSettings["/dbase/Host"] = Host.Text
  Endif
  '# -- Partie variant suivant que l'on utilise un tunnel ssh ou pas
  Start.LocalSettings["/dbase/ssh/Con-ssh"] = Bssh.Value
  If Bssh.value Then
    Start.LocalSettings["/dbase/ssh/Login"] = Loginssh.Text
    Start.LocalSettings["/dbase/ssh/Portssh"] = Portssh.Text
    Start.LocalSettings["/dbase/ssh/Hostssh"] = Hostssh.Text
    Start.LocalSettings["/dbase/ssh/Portlocal"] = Portlocal.Text
  Endif
  Start.LocalSettings["/dbase/Con-ssh"] = Bssh.Value
  Start.LocalSettings["/Soc"] = Soc
  Start.LocalSettings["/intitule"] = intitule
  Start.LocalSettings["/son"] = 0
  Start.LocalSettings["/Tips"] = 1
  Start.LocalSettings["/Soc" & Soc & "/Entete"] = 1
  Start.LocalSettings["/Soc" & Soc & "/Conditions"] = 1
  Start.LocalSettings["/Soc" & Soc & "/Tva550"] = 0
  Start.LocalSettings["/Soc" & Soc & "/Codetva"] = " "
  Start.LocalSettings["/Soc" & Soc & "/Compta"] = 0
  Start.LocalSettings["/Soc" & Soc & "/Gestion"] = 0
  Start.LocalSettings["/Soc" & Soc & "/CptFac"] = 1
  Start.LocalSettings["/Soc" & Soc & "/Qt1"] = 0
  Start.LocalSettings["/Soc" & Soc & "/Coul_fen"] = 0
  Start.LocalSettings["/Soc" & Soc & "/Qt1"] = 0
  Start.LocalSettings["/Soc" & Soc & "/Ctrlstk"] = 0
  Start.LocalSettings["/Soc" & Soc & "/AutoEnt"] = 0
  Start.LocalSettings["/tactile"] = 0
  Start.LocalSettings["/Coul" & "/Fnets"] = "backcolor: &HDDDDDD&; "
  Start.LocalSettings["/Coul" & "/Btns"] = "backcolor: &HBDBDBD&; "
  Start.LocalSettings["/Coul" & "/Znss"] = "backcolor: &HFAFAFA&; "
  Start.LocalSettings["/Coul" & "/Znaff"] = "backcolor: &HEEEEEE&; "
  Start.LocalSettings["/Coul" & "/Focus"] = "backcolor: &HF9FFC7&; "
  Start.LocalSettings["/Coul" & "/Coulfonds"] = "backcolor: &FFEE81&; "
  Start.LocalSettings["/Coul" & "/Others"] = "backcolor: &FFE0B8&; "
  Start.LocalSettings["/Soc" & Soc & "/Copie"] = 0
  Start.LocalSettings["/Soc" & Soc & "/Siret"] = "Siret"
  Start.LocalSettings["/Soc" & Soc & "/Prxdec"] = 0
  Start.LocalSettings["/Soc" & Soc & "/Taxe"] = "TVA"
  Start.LocalSettings.Save

  '# -- valeurs de connexion communes avec ou sans tunnel
  ' Le type reste bien entendu mysql
  hcon.type = Start.LocalSettings["/dbase/type"]
  ' le nom est celui de la base
  hcon.Name = Start.LocalSettings["/dbase/Name"]
  ' le login est celui de la base
  hcon.Login = Start.LocalSettings["/dbase/Login"]
  ' le mot de passe est celui de la base
  hcon.Password = hexdblKey.crypt("de", Start.LocalSettings["/dbase/Password"], "Laurux")

  '*************************************
  '* On initialise les variables
  '* ainsi les routines de connexion
  '* restent inchangées
  '* Il suffira juste de lancer le tunnel
  '* ssh ou pas
  '*************************************
  If Bssh.Value Then
    '# -- Valeurs de connexion via tunnel
    ' par contre on utilise le port local de loopback pour la connexion (et pas localhost qui impliquerait une connexion socket unix)
    hcon.Host = "127.0.0.1"
    ' on remplace le port mysql par le port local du tunnel ssh
    hcon.Port = Start.LocalSettings["/dbase/ssh/Portlocal"]
    ' on définit des variables pour l'établissement du tunnel ssh
    Utils.MysqlServeur = Start.LocalSettings["/dbase/Host"]
    Utils.RemotePort = Start.LocalSettings["/dbase/Port"]
    Utils.SshHost = Start.LocalSettings["/dbase/ssh/Hostssh"]
    Utils.SshPort = Start.LocalSettings["/dbase/ssh/Portssh"]
    Utils.SshUser = Start.LocalSettings["/dbase/ssh/Login"]
  Else
    '# -- Valeurs de connexion sans tunnel
    ' l'hôte à utiliser est le serveur MySQL
    hcon.Host = Start.LocalSettings["/dbase/Host"]
    ' le port le port MySQL du serveur
    If Not Cnl.value Then
      hcon.Port = Start.LocalSettings["/dbase/Port"]
    Else
      hcon.Port = Start.LocalSettings["/dbase/Port2"]
    Endif
  Endif
  Soc = Start.LocalSettings["/Soc"]

  '# -- lancement si nécessaire du tunnel ssh
  If Bssh.Value Then
    If Utils.monteTunnel() Then
      $ready_connect = True
    Else
      $ready_connect = False
    Endif
  Endif

  If $ready_connect Then
    Try hcon.Close

    hcon.Open
    If Message.Info("Le fichier de configuration a été crée ou modifié. Veuillez relancer " & Application.Name & " SVP.", "OK!") = 1 Then
      Button2_Click()
      Quit
    Endif
  Catch
    If Left$(error.Text, 35) = "Cannot open database: Access denied" Then
      Message.Warning("Attention, l' utilisateur ou le mot de passe est erroné ! \n Veuillez controler votre saisie SVP et vérifier que l'utisateur soit bien créé dans MySQL!")
    Else
      If Left$(error.Text, 38) = "Cannot open database: Unknown database" Then
        Message.Warning("Attention, la base n'existe pas sur le serveur sélectionné !")
      Else
        If Left$(error.Text, 35) = "Cannot open database: Can't connect" Then
          Message.Warning("Attention, accès MySQL impossible ! \n Veuillez vérifier que MySQL soit bien installé et que le service soit lancé SVP !")
        Else
          message.Error(Error.Text & " " & Error.Where)
        Endif
      Endif
    Endif
  Else
    Message.Warning("Le tunnel SSH demandé ne peut être monté")
  Endif

End

Public Sub Button2_Click()

  Try Kill User.home & "/" & Application.Name & ".lock"
  Me.close

End

'********************************************************************
'* Fermeture du formulaire avec suppression de l'éventuel tunnel ssh
'* MG - 07 janvier 2010
'********************************************************************
Public Sub Form_Close()

  Utils.fermeTunnel()
  Print Now & " - " & "Fermeture de la fenêtre\n"

End

'******************************************
'* Masquage affichage du cadre tunnel ssh
'* MG - 07 janvier 2010
'******************************************
Public Sub Bssh_Click()

  If Bssh.value Then
    Frame1.Enabled = True
  Else
    Frame1.Enabled = False
  Endif

End
